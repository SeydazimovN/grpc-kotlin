plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.2.61'
    id 'application'
    id 'idea'
    id "com.google.protobuf" version "0.8.5"
}

group 'samgarasx'
version '1.0.0'

apply from: 'versions.gradle'

repositories {
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
    maven { url "https://dl.bintray.com/kotlin/exposed/" }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    compile "io.grpc:grpc-netty-shaded:$grpc_version"
    compile "io.grpc:grpc-protobuf:$grpc_version"
    compile "io.grpc:grpc-stub:$grpc_version"

    implementation "org.jetbrains.exposed:exposed:$exposed_version"
    implementation "org.postgresql:postgresql:$postgresql_version"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

sourceSets {
    main.java.srcDirs += 'src/main/java'
    main.kotlin.srcDirs += 'src/main/kotlin'

    main.java.srcDirs += "$buildDir/generated/source/proto/main/java"
}

idea {
    module.sourceDirs += file("$buildDir/generated/source/proto/main/java")
}

protobuf {
    generatedFilesBaseDir = "$buildDir/generated/source/proto"

    protoc { artifact = 'com.google.protobuf:protoc:3.5.1-1' }

    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:$grpc_version" }
    }

    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

startScripts.enabled = false

task fruitStoreServer(type: CreateStartScripts) {
    mainClassName = 'grpckotlin.FruitStoreServer'
    applicationName = 'fruit-store-server'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = jar.outputs.files + project.configurations.runtime
}

task fruitStoreClient(type: CreateStartScripts) {
    mainClassName = 'grpckotlin.FruitStoreClient'
    applicationName = 'fruit-store-client'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = jar.outputs.files + project.configurations.runtime
}

applicationDistribution.into('bin') {
    from(fruitStoreServer)
    from(fruitStoreClient)
    fileMode = 0755
}
