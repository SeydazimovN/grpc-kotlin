plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.0'
    id 'application'
    id 'idea'
    id "com.google.protobuf" version "0.8.6"
}

group 'samgarasx'
version '1.0.0'

apply from: 'versions.gradle'

repositories {
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
    maven { url "https://dl.bintray.com/kotlin/exposed/" }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    implementation "io.grpc:grpc-netty-shaded:$grpc_version"
    implementation "io.grpc:grpc-protobuf:$grpc_version"
    implementation "io.grpc:grpc-stub:$grpc_version"

    implementation "com.typesafe:config:$typesafe_config_version"
    implementation "com.beust:klaxon:$klaxon_version"
    implementation "org.jetbrains.exposed:exposed:$exposed_version"
    implementation "org.postgresql:postgresql:$postgresql_version"
    implementation "ch.qos.logback:logback-classic:$logback_version"
}

sourceSets {
    main.java.srcDirs += "$buildDir/generated/source/proto/main/java"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

protobuf {
    protoc { artifact = 'com.google.protobuf:protoc:3.5.1-1' }

    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:$grpc_version" }
    }

    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

startScripts.enabled = false

task fruitStoreServer(type: CreateStartScripts) {
    mainClassName = 'grpckotlin.FruitStoreServer'
    applicationName = 'fruit-store-server'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = jar.outputs.files + project.configurations.runtime
}

task fruitStoreClient(type: CreateStartScripts) {
    mainClassName = 'grpckotlin.FruitStoreClient'
    applicationName = 'fruit-store-client'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = jar.outputs.files + project.configurations.runtime
}

applicationDistribution.into('bin') {
    from(fruitStoreServer)
    from(fruitStoreClient)
    fileMode = 0755
}
